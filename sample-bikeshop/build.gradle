/*
 * Copyright (C) 2016 Square, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion

  repositories {
    mavenLocal()
    mavenCentral()
    google()
  }

  defaultConfig {
    applicationId "com.example.owensbikes"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.compileSdkVersion
    versionCode 1
    versionName "1.0"
  }

  buildTypes {
      debug {
        project.addClientIdField(buildTypes.debug)
      }
      release {
        minifyEnabled false
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        project.addClientIdField(buildTypes.release)
      }
    }
}

dependencies {
  implementation 'androidx.appcompat:appcompat:1.6.1'
  implementation 'androidx.recyclerview:recyclerview:1.3.0'
  implementation 'com.google.code.gson:gson:2.10.1'
  implementation "com.squareup.sdk:point-of-sale-sdk:2.1"
}



void addClientIdField(def buildType) {
  if (file('./bikeshop.properties').exists()) {
    def props = new Properties()
    props.load(new FileInputStream(file('bikeshop.properties')))
    buildType.buildConfigField "String", "CLIENT_ID", props['clientId']
  } else {
    throw new GradleException('Please create bikeshop.properties, ' +
        'see bikeshop.properties.example')
  }
}